apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
group= 'com.github.roadhouse-dev'


android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task(type: Zip, "zipTestReport") {
    from("${rootDir.absolutePath}/library/build/reports/androidTests/connected/")
    archiveName 'test_report_library.zip'
    destinationDir file("${rootDir.absolutePath}/library/build/reports/")
}

configure(zipTestReport) {
    group = 'verification'
    description = 'Create a test report archive'
}

task(type: Zip, "zipLintReport") {
    from "${rootDir.absolutePath}/library/build/outputs/"
    include "lint-results-debug.html"
    include "lint-results-debug_files/*"
    archiveName 'lint_report_library.zip'
    destinationDir file("${rootDir.absolutePath}/library/build/reports/")
}

configure(zipLintReport) {
    group = 'verification'
    description = 'Create a lint report archive'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'io.reactivex:rxjava:1.1.4'
    compile 'com.github.Raizlabs.DBFlow:dbflow:3.1.1'
    compile 'com.android.support:appcompat-v7:24.2.1'
    testCompile 'junit:junit:4.12'
}

